// Configures publishing of Maven artifacts to Bintray

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

// --------------- pom configuration ---------------

def pomConfig = {
    description "AtomicFU utilities"
    url "https://github.com/Kotlin/atomicfu"

    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "JetBrains"
            name "JetBrains Team"
            organization "JetBrains"
            organizationUrl "http://www.jetbrains.com"
        }
    }

    scm {
        url "https://github.com/Kotlin/atomicfu"
    }
}

// ------------- tasks

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from "src/main/kotlin"
}

if (!project.name.endsWith("-native")) {
    publishing {
        repositories {
            maven { url = 'https://kotlin.bintray.com/kotlinx' }
        }
        publications {
            maven(MavenPublication) {
                from components.java
                artifact sourcesJar
                if (project.name.endsWith("-maven-plugin")) {
                    pom.with {
                        packaging = 'maven-plugin'
                    }
                }
                pom.withXml {
                    def root = asNode()
                    root.appendNode('name', project.name)
                    root.children().last() + pomConfig
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    pkg {
        userOrg = 'kotlin'
        repo = 'kotlinx'
        name = 'kotlinx.atomicfu'
        version {
            name = project.version
            vcsTag = project.version
            released  = new Date()
        }
    }
}

// NOTE: Must do publishToMavenLocal first to generate .module files
bintrayUpload.dependsOn publishToMavenLocal

bintrayUpload.doFirst {
    publications = project.publishing.publications.findAll { !it.name.contains('-test') }.collect {
        println("Uploading artifact '$it.groupId:$it.artifactId:$it.version' from publication '$it.name'")
        it
    }
}